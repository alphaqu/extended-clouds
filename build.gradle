plugins {
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id "com.modrinth.minotaur" version "2.+"

    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.1-SNAPSHOT" apply false

    id "io.github.pacifistmc.forgix" version "1.2.6"

    id 'maven-publish'
}

architectury {
    minecraft = rootProject.minecraft_version
}
subprojects {
    apply plugin: "dev.architectury.loom"


    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings "net.fabricmc:yarn:${rootProject.yarn_mappings}:v2"
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}

forgix {
    group = rootProject.maven_group
    mergedJarName = rootProject.archives_base_name + "-" + project.mod_version + ".jar"
    outputDir = "build/libs/Merged"
}


String getFileName() {
    return "build/libs/Merged/" + rootProject.archives_base_name + "-" + project.mod_version + ".jar";
}

tasks.register("buildMod") {
    dependsOn 'fabric:build'
    dependsOn 'forge:build'
    finalizedBy 'mergeJars'
}

modrinth {
    token = project.hasProperty("modrinthApiKey") ? project.modrinthApiKey : ""
    projectId = 'SRvY3a39'
    changelog = file("changelog.md").getText()
    versionNumber = project.version
    versionName = "$project.version".split("\\+")[0] + " for $project.minecraft_version"
    uploadFile = getFileName()
    versionType = "release"
    gameVersions = ['1.20']
    loaders = ['fabric', 'quilt', 'forge']
}

curseforge {
    apiKey = project.hasProperty("curseForgeApiKey") ? project.curseForgeApiKey : ""
    project {
        id = '624368'
        changelogType = "markdown"
        changelog = file("changelog.md")
        releaseType = 'release'

        addGameVersion "1.20"
        addGameVersion "Forge"
        addGameVersion "Fabric"
        addGameVersion "Quilt"
        addGameVersion "Java 17"

        mainArtifact(getFileName()) {
            displayName = "$project.version".split("\\+")[0] + " for $project.minecraft_version"
        }
    }
    options {
        forgeGradleIntegration = false
    }
}

tasks.register("publishMod") {
    dependsOn buildMod
    finalizedBy 'modrinth'
    finalizedBy 'curseforge'
}

// configure the maven publication
publishing {
    repositories {
        maven {
            name = "notalpha"
            url = "https://notalpha.dev/maven/releases"
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}
